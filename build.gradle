group 'com.harish'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

// Add Gradle shadow jar Plugin, see https://github.com/johnrengelman/shadow
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

ext {
    dropwizardVersion = '1.3.5'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    implementation 'org.twitter4j:twitter4j-core:4.0.6'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.2'

    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
}

// Configure the shadow jar task
shadowJar {
    baseName = 'social-data-analytics-service'
    classifier = null
    version = 'all'
    manifest {
        attributes 'Main-Class': 'com.harish.sdas.SocialDataAnalyticsService'
    }
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

assemble.dependsOn shadowJar